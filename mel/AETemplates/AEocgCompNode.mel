//
// Copyright (C) 2021 David Cattermole.
//
// This file is part of OpenCompGraphMaya.
//
// OpenCompGraphMaya is free software: you can redistribute it and/or modify it
// under the terms of the GNU Lesser General Public License as
// published by the Free Software Foundation, either version 3 of the
// License, or (at your option) any later version.
//
// OpenCompGraphMaya is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with OpenCompGraphMaya.  If not, see <https://www.gnu.org/licenses/>.
// ---------------------------------------------------------------------



global proc AEocgCompNodeSelectImagePlaneCommand(string $nodeName)
{
    string $nodes[] = `listHistory -future true -breadthFirst $nodeName`;

    string $imagePlaneNode = "";
    string $node;
    for ($node in $nodes) {
        string $nodeType = `nodeType $node`;
        if ($nodeType == "ocgImagePlane") {
            $imagePlaneNode = $node;
            break;
        }
    }
    if (($imagePlaneNode != "") && objExists($imagePlaneNode)) {
        select -r $imagePlaneNode;
        showEditorExact $imagePlaneNode;
    }
}


global proc AEocgCompNodeSelectImagePlaneReplace(string $nodeName)
{
    string $buffer[];
    int $numTokens = `tokenize $nodeName "." $buffer`;
    $nodeName = $buffer[0];

    button -e
        -command ("AEocgCompNodeSelectImagePlaneCommand " + $nodeName)
        ocgSelectImagePlaneNodeEditorButton;
}


global proc AEocgCompNodeSelectImagePlaneNew(string $nodeName)
{
    setUITemplate -pst attributeEditorTemplate;
    rowLayout -nc 3;

        text -label "" ;
        button -label ("Select Image Plane") ocgSelectImagePlaneNodeEditorButton;
        text -label "" ;

    setParent ..;
    setUITemplate -ppt;

    AEocgCompNodeSelectImagePlaneReplace $nodeName;
}


global proc AEocgCompNodeButton(string $nodeName)
{
    // Add button to select the connected ImagePlane;
    editorTemplate
        -callCustom "AEocgCompNodeSelectImagePlaneNew" "AEocgCompNodeSelectImagePlaneReplace"
        $nodeName;
}
